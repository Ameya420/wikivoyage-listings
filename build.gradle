
plugins {
    id 'java'
    id 'org.hidetake.ssh' version '1.1.4'
}

dependencies {
    compile fileTree(dir: 'lib', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.wikivoyage.ru.listings.Main'
    }
}

task distZip(type: Zip) {
    from jar.outputs.files
    from('lib/') {
        include '**/*.jar'
        into('lib')
    }
    from('config/') {
        include '**/*.*'
        into('config')
    }
    from('.') {
        include 'wikivoyage-listings.sh'
        into('.')
    }
}

remotes {
    webServer {
        host = '109.120.189.189'
        user = 'alexey'
        identity = file('/home/alexey/.ssh/id_rsa')
        knownHosts = allowAnyHosts
    }
}

task deploy << {
    ssh.run {
        session(remotes.webServer) {
            execute 'rm -f wikivoyage-listings.zip'
            execute 'rm -rf wikivoyage-listings'
            put 'build/distributions/wikivoyage-listings.zip', 'wikivoyage-listings.zip'
            execute 'mkdir wikivoyage-listings'
            execute 'cd wikivoyage-listings && unzip ../wikivoyage-listings.zip'
        }
    }
}
deploy.dependsOn distZip
